apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = new File(rootProject.projectDir, 'keystore.properties')

android {
    compileSdkVersion 32
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "org.sil.storyproducer"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 27
        versionName '3.2.2'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        if (keystorePropertiesFile.isFile()) {
            logger.lifecycle("keystore.properties exists. Sign APK.")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        else
        {
            logger.lifecycle("keystore.properties not found. Unable to sign APK.")
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionPart = variant.name;
            if (variant.name != "debug") {
                if (keystorePropertiesFile.isFile()) {
                    versionPart = variant.versionName;
                } else {
                    versionPart = versionPart + "-not-signed";
                }
            }
            outputFileName = "SP-${versionPart}.apk"
        }
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.isFile()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
        }
        // dkh - 5/3/21  - These changes came  from Chris Hubbard (pull request 568)
        // Updated applicationIdSuffix to allow debug versions of app crashes to be captured separate from
        // continuous build app crashes or product builds app crashes in Firebase.
        // Also, make sure we capture analytics by
        // setting FIREBASE_ANALYTICS_DEACTIVATED to false.
        //"package_name" in debug\google-services.json was also changed.  Side effect of
        // changing the package_name is that field names in the JSON registration parsing were
        // affected.  See Issue #559 for a more detailed explanation.
        debug {
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
        }
        continuous {
            initWith release
            applicationIdSuffix ".ci"
        }
    }
    productFlavors {
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildFeatures {
        viewBinding true
    }
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    google()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.10.1'
    implementation 'com.github.futuresimple:android-floating-action-button:1.10.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.22'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.arthenica:mobile-ffmpeg-full:4.2.LTS'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.firebase:firebase-analytics:17.6.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'
    implementation 'com.github.codekidX:storage-chooser:2.0.3'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.opencsv:opencsv:4.3.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.webkit:webkit:1.1.0'
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'net.lingala.zip4j:zip4j:2.3.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // DKH - 08/26/2021 Added by Cedarville for Wordlinks
    // implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    // Issue 166, Cedarville pull #519
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'

    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.6.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

// Added the following 'configurations' exclusions to fix issue: build.gradle: Error: commons-logging defines classes that conflict with classes now provided by Android.
// Solutions include finding newer versions or alternative libraries that don't have the same problem (for example, for httpclient use HttpUrlConnection or okhttp instead), or repackaging the library using something like jarjar.
// [DuplicatePlatformClasses]
// As suggested by: https://stackoverflow.com/questions/46989310/commons-logging-defines-classes-that-conflict-with-classes-now-provided-by-andro
configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

