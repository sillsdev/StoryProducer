apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
//    signingConfigs {
//        TestSign {
//            keyAlias '12345'
//            keyPassword '12345'
//            storeFile file('C:/Users/Dell/Documents/StoryProducer/TestSign')
//            storePassword '12345'
//        }
//    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "org.sil.storyproducer"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 9
        versionName '2.3.52alpha'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            versionNameSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ROCC_URL_PREFIX", ROCC_URL_PREFIX
            buildConfigField "String", "ROCC_WEBSOCKETS_PREFIX", ROCC_WEBSOCKETS_PREFIX
            buildConfigField "Boolean", "ENABLE_IN_APP_ROCC_URL_SETTING", "false"
            resValue "bool", "enable_in_app_rocc_url_setting", "false"
        }
        debug {
            versionNameSuffix ".debug"
            buildConfigField "String", "ROCC_URL_PREFIX", ROCC_URL_PREFIX
            buildConfigField "String", "ROCC_WEBSOCKETS_PREFIX", ROCC_WEBSOCKETS_PREFIX
            buildConfigField "Boolean", "ENABLE_IN_APP_ROCC_URL_SETTING", "true"
            resValue "bool", "enable_in_app_rocc_url_setting", "true"
        }
        clientDebug {
            initWith debug
            buildConfigField "Boolean", "ENABLE_IN_APP_ROCC_URL_SETTING", "false"
            resValue "bool", "enable_in_app_rocc_url_setting", "false"

        }
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
    productFlavors {
    }
    //TODO remove the "pick first" - it is just to resolve a bug in the coroutines dependancy.
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    google()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.10.1'
    implementation 'com.github.futuresimple:android-floating-action-button:1.10.1'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.22'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.arthenica:mobile-ffmpeg-full:4.2.LTS'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.codekidX:storage-chooser:2.0.3'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.opencsv:opencsv:4.3.2'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'org.java-websocket:Java-WebSocket:1.4.1'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.6.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

apply plugin: 'com.google.gms.google-services'
// TODO remove the code below when appropriate
// The following line avoids an ArrayIndexOutOfBounds exception when running `./gradlew build`
// The version check appears to be problematic in Google Services Plugin 4.2.0.
// (see https://github.com/google/play-services-plugins/issues/47)
// This workaround came from Fabian Streitel's answer to the following SO post:
// https://stackoverflow.com/questions/54441456/cant-update-to-com-google-gmsgoogle-services4-2-0
// Hopefully the corresponding GitHub issue will resolve this issue, so that this version check can
// be removed in the future.
com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true
